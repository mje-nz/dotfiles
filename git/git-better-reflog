#!/usr/bin/env zsh
# Massage the output from git reflog into a more helpful format:
# * Display the index-format reflog designator (master@{0}) but also the
#   timestamp from the timestamp-format reflog designator (master@{1 minute ago}).
# * Use descriptive commit names in reflog statuses.
# * Include commit status, author and date if they aren't in the reflog status.
#
# Author: Matthew Edwards

# Enable PCRE for ".*?"
emulate -L zsh
setopt RE_MATCH_PCRE
# Make "local" work in loops
setopt TYPESET_SILENT

# Use colour if stdout is a tty
if [[ -t 1 ]]; then
	color="--color"
	dim="$(tput dim)"
	hash_color="$(tput setaf 3)"
	reset="$(tput sgr0)"
fi

# Pretty reflog command prefixed with bare commit hash
main_reflog() {
	git reflog "$color" "--format=%h %Cred%gd%C(auto): %h %gs" "$1@{0}"
}
# Second reflog command to get timestamp-format designator and commit info
extra_reflog() {
	git reflog "$color" "--format=%h %gd %s (%C(blue)%an%C(yellow), %C(green)%cr%C(white))" "$1@{now}"
}
# Regexes for parsing output
main_regex='(\w+) (.*?@{\d+}.*?): (.+)'
extra_regex='(\w+) .*?@{(.*?)} (.*) (\(.*\))'
moving_regex='(.*? moving from) (\S+?) to (\S+)'
rebase_onto_regex='^(\S+ rebase .*?finish.+?:) (\S+?) onto (\S+)'
rebase_returning_regex='^(\S+ rebase finished: returning to) (\S+)'
reset_regex='^(\S+ reset: moving to) (\S+)'

# Look up descriptive name for revision
declare -A rev_names
get_name() {
	local rev=$1 output=$2

	# Skip if already short
	[[ ${#rev} -lt 15 ]] && return

	# Optimisation: fill cache if empty
	if [[ ${#rev_names} -eq 0 ]]; then
		git for-each-ref --format='%(objectname) %(refname:short)' |
		while read -r commit name; do
		    rev_names[$commit]=$name
		done
	fi

	# Shorten name
	if (( ${+rev_names[$rev]} == 0 )); then
		rev_names[$rev]="$(git name-rev --exclude="remotes/*" --name-only --no-undefined --always "$rev" 2> /dev/null)"
		rev_names[$rev]="${rev_names[$rev]#tags/}"
	fi
	eval "$output=\"${rev_names[$rev]}\""
}

# Read from both reflog commands line-by-line and combine them
main() {
	local designator_length
	while read -r main <&3 && read -r extra <&4; do
		[[ "$main" =~ $main_regex ]] || return 255
		# shellcheck disable=SC2154
		local commit="${match[1]}" designator="${match[2]}" body="${match[3]}"
		[[ "$extra" =~ $extra_regex ]] || return 254
		local extra_commit="${match[1]}" extra_time="${match[2]}"
		local extra_status="${match[3]}" extra_info="${match[4]}"
		[[ "$commit" == "$extra_commit" ]] || return 253

		# Handle specific messages
		if [[ "$body" =~ $moving_regex ]]; then
			# Abbreviate commits in "moving from x to y" messages
			local start="${match[1]}" ref1="${match[2]}" ref2="${match[3]}"
			local end="${match[4]}"
			get_name "$ref1" ref1 || return 252
			get_name "$ref2" ref2 || return 251
			# shellcheck disable=SC2154
			body="$start $hash_color$ref1$reset to $hash_color$ref2$reset $end"
		elif [[ "$body" =~ $rebase_onto_regex ]]; then
			# Abbreviate commit in "rebase finished: x onto y" messages
			local start="${match[1]}" ref1="${match[2]}" ref2="${match[3]}"
			get_name "$ref2" ref2 || return 250
			# shellcheck disable=SC2154
			body="$start $hash_color$ref1$reset onto $hash_color$ref2$reset"
		elif [[ "$body" =~ $rebase_returning_regex || "$body" =~ $reset_regex ]]; then
			# Highlight commit in "rebase finished: returning to x" and
			# "reset: moving to x" messages
			local start="${match[1]}" ref="${match[2]}"
			# shellcheck disable=SC2154
			body="$start $hash_color$ref$reset"
		fi

		if [[ "$body" != *"$extra_status" ]]; then
			# Print commit status when it isn't in the reflog status
			body="$body ${dim}[$extra_status $extra_info]$reset"
		fi


		if [[ -z "$designator_length" ]]; then
			if [[ $(git rev-list --count --walk-reflogs HEAD) -ge 10 ]]; then
				# If there are ten or more reflog entries, pad designator field
				# to one longer than the first one we see so HEAD@{0} lines up
				# with HEAD@{10}
		 		(( designator_length = ${#designator} + 1 ))
		 	else
		 		designator_length=${#designator}
		 	fi
		fi

		printf "%-*s %-15s %s\n" "$designator_length" "$designator" "($extra_time):" "$body"
	done 3< <(main_reflog "$1") 4< <(extra_reflog "$1")
}

if [[ $# -gt 1 || "$1" == *@* ]]; then
	echo "usage: git better-reflog [branch]" >&2
	exit 1
fi

if [[ -t 1 ]]; then
	# Basically the git pager
	main "$1" | eval "$(git var GIT_PAGER)"
else
	set -x
	main "$1"
fi

# TODO: handle gref | cat
# TODO: put into own repo like https://github.com/unixorn/git-extra-commands
